import net.fabricmc.loom.task.RunClientTask
import net.fabricmc.loom.task.RunServerTask
import java.time.Year

plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.5.0'
}

group 'com.hrznstudio'
version "0.3.0-prealpha+${project.mc}"
archivesBaseName = 'GalacticraftAPI'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    maven {
        url "https://dl.bintray.com/ladysnake/libs"
        content {
            includeGroup "io.github.onyxstudios.Cardinal-Components-API"
        }
    }
}

loom {
    refmapName = "galacticraft-api.refmap.json"
    accessWidener "src/main/resources/galacticraft-api.accesswidener"
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.mc}"
    mappings "net.fabricmc:yarn:${project.mc}+build.${project.yarn}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader}"
	
	modImplementation fabricApi.module("fabric-api-base", project.fabric)
    modImplementation fabricApi.module("fabric-command-api-v1", project.fabric)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric)
    modImplementation fabricApi.module("fabric-registry-sync-v0", project.fabric)
    modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric)

    testmodCompile sourceSets.main.output
}

configurations {
    testmodCompile
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier  = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}


task runTestmodClient(type: RunClientTask) {
    classpath sourceSets.testmod.runtimeClasspath
}

task runTestmodServer(type: RunServerTask) {
    classpath sourceSets.testmod.runtimeClasspath
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                "Implementation-Title"    : "GalacticraftAPI",
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "HRZN LTD",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Maven-Artifact"          : "${project.group}:GalacticraftAPI:${project.version}",
                "ModSide"                 : "BOTH",
        ],)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			groupId = 'com.hrznstudio'
			artifactId = 'GalacticraftAPI'
            artifact(file("${project.buildDir}/libs/${project.archivesBaseName}-${project.version}.jar")) { // thanks shedaniel
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
//        maven {
//            url "s3://cdn.hrzn.studio/maven"
//            authentication {
//                awsIm(AwsImAuthentication)
//            }
//        }
    }
}

license {
    header = project.file("LICENSE_HEADER.txt")
    include '**/com/hrznstudio/**/*.java'
    include 'build.gradle'
    ext {
        year = Year.now().getValue()
        company = "HRZN LTD"
    }
}